apply plugin: 'com.jfrog.bintray'

def getPublications = {
    def publications = []
    project.publishing.publications.all {
        publications.add(it.name)
    }

    return publications
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    dryRun = false

    pkg {
        userOrg = projectBintrayUserOrg
        repo = projectBintrayRepoName
        name = projectBintrayArtifactName
        licenses = ['Apache-2.0']
        publications = getPublications()

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version

            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_PASSPHRASE')
            }
        }
    }
}

if (plugins.hasPlugin("org.jetbrains.kotlin.multiplatform")) {
    bintrayUpload {
        doFirst {
            publishing.publications.findAll {
                it instanceof MavenPublication
            }.each {
                it.artifact("$buildDir/publications/$it.name/module.json") {
                    extension 'module'
                }
            }
        }
    }
}
